plugins {
    id 'base'
    id "de.undercouch.download" version "3.2.0"
}

import de.undercouch.gradle.tasks.download.Download

task downloadPromptList(type: Download) {
    src 'https://catalog.ldc.upenn.edu/docs/LDC93S1/PROMPTS.TXT'
    dest 'build/prompts.txt'
    overwrite false
}

task downloadRevealJS(type: Download) {
    src 'https://github.com/hakimel/reveal.js/archive/3.4.1.zip'
    dest 'build/reveal.js.zip'
    overwrite false
}

task unpackRevealJS(type: Copy) {
    dependsOn downloadRevealJS
    from zipTree('build/reveal.js.zip')
    into 'build'
    eachFile {
        it.path = it.path - '-3.4.1'
    }
}

task generateMarkdown {
    dependsOn downloadPromptList
    ext.markdownFile = file('build/prompts.md')
    outputs.files markdownFile
    doLast {
        file('build/prompts.md').withWriter { md ->
            md.println '# TIMIT'
            file('build/prompts.txt').eachLine { line ->
                if (!line.startsWith(';')) {
                    md.println "## $line"
                }
            }
        }
    }
}

task compileHtml(type: Exec) {
    def markdownFile = file('prompts.md')
    commandLine 'pandoc', '-t', 'revealjs', '--standalone', markdownFile, '-o', 'prompts.html'
    workingDir 'build'
}
